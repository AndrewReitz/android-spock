apply plugin: 'com.android.library'
apply plugin: 'groovyx.grooid.groovy-android'

android {
  compileSdkVersion project.compileSdkVersion
  buildToolsVersion project.buildToolsVersion

  defaultConfig {
    minSdkVersion project.minSdkVersion
    targetSdkVersion project.compileSdkVersion
    versionCode 1
    versionName project.version

    testInstrumentationRunner "com.andrewreitz.spock.AndroidSpockRunner"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_6
    targetCompatibility JavaVersion.VERSION_1_6
  }

  lintOptions {
    abortOnError false
  }

  packagingOptions {
    exclude 'META-INF/services/org.codehaus.groovy.transform.ASTTransformation'
    exclude 'LICENSE.txt'
  }

  // Android plugin is doing something after the fact to remove this so add it back
  sourceSets {
    androidTest.java.srcDir 'src/androidTest/groovy'
  }

  dexOptions {
    preDexLibraries false
    incremental false
    javaMaxHeapSize "2G"
  }
}

tasks.withType(com.android.build.gradle.tasks.Dex) {
  // In order to include Introspector implementation
  additionalParameters=['--core-library']
}

dependencies {
  compile 'org.codehaus.groovy:groovy:2.4.0:grooid'

  compile 'junit:junit-dep:4.11'

  compile('com.android.support.test:testing-support-lib:0.1') {
    exclude group: 'junit'
  }

  compile('org.spockframework:spock-core:0.7-groovy-2.0') {
    exclude group: 'org.codehaus.groovy'
    exclude group: 'junit'
  }

  compile 'cglib:cglib-nodep:3.1'
}

apply from: "$rootDir/gradle/compile.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/bintray.gradle"
apply from: "$rootDir/gradle/artifactory.gradle"
