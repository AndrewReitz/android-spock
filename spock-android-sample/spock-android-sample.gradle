apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'

repositories {
  jcenter()
  maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
}

android {
  compileSdkVersion project.compileSdkVersion
  buildToolsVersion project.buildToolsVersion

  defaultConfig {
    minSdkVersion project.minSdkVersion
    targetSdkVersion project.compileSdkVersion
    versionCode 1
    versionName project.version

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  packagingOptions {
    exclude 'META-INF/services/org.codehaus.groovy.transform.ASTTransformation'
    exclude 'LICENSE.txt'
  }

  buildTypes {
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }
  }

  // in order to bring memory usage down on CI
  dexOptions {
    incremental false
    preDexLibraries false
    javaMaxHeapSize "1538M"
  }

  lintOptions {
    abortOnError false
  }
}

// Used for forcing to get the latest snapshot of spock-android very build
configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
  compile 'org.codehaus.groovy:groovy:2.4.4:grooid'

  androidTestCompile project(':spock-android')
  androidTestCompile "com.andrewreitz:spock-android:$project.version${isSnapshot ? "" : "-SNAPSHOT"}"
  androidTestCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
    exclude group: 'org.codehaus.groovy'
    exclude group: 'junit'
  }
  androidTestCompile 'com.android.support.test:runner:0.3'
  androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
}
