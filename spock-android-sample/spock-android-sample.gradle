apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'

repositories {
  jcenter()
  maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
}

android {
  compileSdkVersion project.compileSdkVersion
  buildToolsVersion project.buildToolsVersion

  defaultConfig {
    minSdkVersion project.minSdkVersion
    targetSdkVersion project.compileSdkVersion
    versionCode 1
    versionName project.version

    testInstrumentationRunner "com.andrewreitz.spock.AndroidSpockRunner"
  }

  packagingOptions {
    exclude 'META-INF/services/org.codehaus.groovy.transform.ASTTransformation'
    exclude 'LICENSE.txt'
  }

  buildTypes {
    debug {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }
  }

  // in order to bring memory usage down on CI
  dexOptions {
    incremental false
    preDexLibraries false
    javaMaxHeapSize "1G"
  }

  lintOptions {
    abortOnError false
  }
}

// Used for forcing to get the latest snapshot of spock-android very build
configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
  compile 'org.codehaus.groovy:groovy:2.4.0:grooid'

  androidTestCompile "com.andrewreitz:spock-android:$project.version${isSnapshot ? "" : "-SNAPSHOT"}"
  androidTestCompile 'junit:junit-dep:4.11'
  androidTestCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
    exclude group: 'org.codehaus.groovy'
    exclude group: 'junit'
  }
  androidTestCompile('com.android.support.test:testing-support-lib:0.1') {
    exclude group: 'junit'
  }
  androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
}
